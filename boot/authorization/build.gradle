plugins {
    id 'java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'org.graalvm.buildtools.native'
    id 'org.hibernate.orm' version '6.4.2.Final'
}

ext['hibernate.version']='6.4.2.Final'

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.named("bootBuildImage") {
    tags = ["${dockerPrefix}/${rootProject.name}-${project.name}:latest"]
    imageName = ("${dockerPrefix}/${rootProject.name}-${project.name}:${project.version}")
    environment = [
            "BPE_DELIM_JAVA_TOOL_OPTIONS" : " ",
            "BPE_APPEND_JAVA_TOOL_OPTIONS": "-Dfile.encoding=UTF-8"
    ]
    buildWorkspace {
        bind {
            source = "cache.${rootProject.name}-${project.name}.work"
        }
    }
    buildCache {
        bind {
            source = "cache.${rootProject.name}-${project.name}.build"
        }
    }
    launchCache {
        bind {
            source = "cache.${rootProject.name}-${project.name}.launch"
        }
    }
    publish = true
    docker {
        publishRegistry {
            username = "${dockerUsername}"
            password = "${dockerPassword}"
            email = "${dockerEmail}"
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.postgresql:postgresql'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    implementation("org.springframework.boot:spring-boot-starter-security")

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}