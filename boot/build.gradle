plugins {
    id 'org.springframework.boot' version '4.0.0-M3' apply false
    id 'org.graalvm.buildtools.native' version '0.11.1' apply false
    id 'io.spring.dependency-management' version '1.1.7'
}

ext['flyway.version'] = '11.13.2'
ext['r2dbc-postgresql.version'] = '1.1.0.RELEASE'

configure(allprojects) { project ->

    group "com.alex.plate"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

configure(subprojects) { project ->

    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    tasks.named("bootBuildImage") {
        createdDate = "now"
        imageName = "localhost:5000/${rootProject.name}-${project.name}:latest"
        environment = [
                "BPE_DELIM_JAVA_TOOL_OPTIONS" : " ",
                "BPE_APPEND_JAVA_TOOL_OPTIONS": "-Dfile.encoding=UTF-8 -XX:+HeapDumpOnOutOfMemoryError" +
                        "-Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager"
        ]
        buildCache {
            volume {
                name = "cache-${rootProject.name}-${project.name}.build"
            }
        }
        launchCache {
            volume {
                name = "cache-${rootProject.name}-${project.name}.launch"
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        implementation 'com.google.guava:guava:33+'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-json'
        implementation 'org.springframework.boot:spring-boot-starter-jackson'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        modules {
            module("org.springframework.boot:spring-boot-starter-logging") {
                replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
            }
        }

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}